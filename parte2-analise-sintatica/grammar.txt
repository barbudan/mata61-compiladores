INICIO -> PROTOTIPO_FUNC

PROTOTIPO_FUNC -> TIPO_FUNC id ( LISTA_VARS1 ; PROTOTIPO_FUNC
PROTOTIPO_FUNC -> FUNC

FUNC -> TIPO_FUNC id ( LISTA_VARS1 { BLOCO_MAIN FUNC
FUNC -> INT_MAINHA

INT_MAINHA -> int mainha ( ) { BLOCO_MAIN

BLOCO_MAIN -> DECLARACAO

DECLARACAO -> TIPO_VAR id ; DECLARACAO
DECLARACAO -> TIPO_VAR id = EXP_ARITMETICA1 ; DECLARACAO
DECLARACAO -> TIPO_VAR id [ EXP_ARITMETICA1 ] ; DECLARACAO
DECLARACAO -> BLOCO

BLOCO -> ATRIBUICAO BLOCO
BLOCO -> DESVIO_CONDICIONAL BLOCO
BLOCO -> LOOP BLOCO
BLOCO -> LEITURA BLOCO
BLOCO -> ESCRITA BLOCO
BLOCO -> FUNC_NADA BLOCO
BLOCO -> TOME BLOCO
BLOCO -> VAZA BLOCO
BLOCO -> }

ATRIBUICAO -> id [ EXP_ARITMETICA1 ] = EXP_ARITMETICA1 ;
ATRIBUICAO -> id = EXP_ARITMETICA1 ;

DESVIO_CONDICIONAL -> se ( EXP_COMP1 ) { DESVIO_CONDICIONAL2
DESVIO_CONDICIONAL2 -> BLOCO senao { BLOCO
DESVIO_CONDICIONAL2 -> BLOCO

LOOP -> uai ( EXP_COMP1 ) { BLOCO

LEITURA -> le ( id ) ;
LEITURA -> le ( id [ EXP_ARITMETICA1 ] ) ;

ESCRITA -> escreve ( EXP_ARITMETICA1 ) ;
ESCRITA -> escreve ( string ) ;

FUNC_NADA -> id ( FUNC_ARG1 ;

TOME -> tome ;
TOME -> tome EXP_ARITMETICA1 ;

VAZA -> vaza ;

EXP_ARITMETICA1 -> EXP_ARITMETICA1 + EXP_ARITMETICA2
EXP_ARITMETICA1 -> EXP_ARITMETICA1 - EXP_ARITMETICA2
EXP_ARITMETICA1 -> EXP_ARITMETICA2
EXP_ARITMETICA2 -> EXP_ARITMETICA2 * EXP_ARITMETICA3
EXP_ARITMETICA2 -> EXP_ARITMETICA2 / EXP_ARITMETICA3
EXP_ARITMETICA2 -> EXP_ARITMETICA2 % EXP_ARITMETICA3
EXP_ARITMETICA2 -> EXP_ARITMETICA3
EXP_ARITMETICA3 -> - EXP_ARITMETICA4
EXP_ARITMETICA3 -> EXP_ARITMETICA4
EXP_ARITMETICA4 -> ( EXP_ARITMETICA1 )
EXP_ARITMETICA4 -> id
EXP_ARITMETICA4 -> number
EXP_ARITMETICA4 -> char
EXP_ARITMETICA4 -> id [ EXP_ARITMETICA1 ]
EXP_ARITMETICA4 -> id ( FUNC_ARG1

FUNC_ARG1 -> EXP_ARITMETICA1 FUNC_ARG2
FUNC_ARG1 -> )

FUNC_ARG2 -> , EXP_ARITMETICA1 FUNC_ARG2
FUNC_ARG2 -> )

EXP_RELACIONAL -> EXP_ARITMETICA1 == EXP_ARITMETICA1
EXP_RELACIONAL -> EXP_ARITMETICA1 != EXP_ARITMETICA1
EXP_RELACIONAL -> EXP_ARITMETICA1 >= EXP_ARITMETICA1
EXP_RELACIONAL -> EXP_ARITMETICA1 <= EXP_ARITMETICA1
EXP_RELACIONAL -> EXP_ARITMETICA1 > EXP_ARITMETICA1
EXP_RELACIONAL -> EXP_ARITMETICA1 < EXP_ARITMETICA1

EXP_COMP1 -> EXP_COMP1 ou EXP_COMP2
EXP_COMP1 -> EXP_COMP2
EXP_COMP2 -> EXP_COMP2 e EXP_COMP3
EXP_COMP2 -> EXP_COMP3
EXP_COMP3 -> nao EXP_COMP3
EXP_COMP3 -> EXP_COMP4
EXP_COMP4 -> ( EXP_COMP1 )
EXP_COMP4 -> EXP_RELACIONAL

TIPO_FUNC -> int
TIPO_FUNC -> nada
TIPO_FUNC -> real
TIPO_FUNC -> letra

TIPO_VAR -> int
TIPO_VAR -> real
TIPO_VAR -> letra

LISTA_VARS1 -> TIPO_VAR id LISTA_VARS2
LISTA_VARS1 -> TIPO_VAR id [ ] LISTA_VARS2
LISTA_VARS1 -> TIPO_VAR id [ EXP_ARITMETICA1 ] LISTA_VARS2
LISTA_VARS1 -> )

LISTA_VARS2 -> , TIPO_VAR id LISTA_VARS2
LISTA_VARS2 -> , TIPO_VAR id [ ] LISTA_VARS2
LISTA_VARS2 -> , TIPO_VAR id [ EXP_ARITMETICA1 ] LISTA_VARS2
LISTA_VARS2 -> )

